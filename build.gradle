version = '0.0.1-SNAPSHOT'
group = 'net.weaz'

buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        springPlatformVersion = '2.0.3.RELEASE'
        springPlatformPluginVersion = '0.5.4.RELEASE'
        springCloudVersion = 'Brixton.M4'

        nodeVersion = '4.4.2'
        npmVersion = '3.8.5'

        flywayPluginVersion = '3.2.1'
        nodePluginVersion = '0.12'
        gruntPluginVersion = '0.12'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.flywaydb:flyway-gradle-plugin:${flywayPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${springPlatformPluginVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:${nodePluginVersion}")
        classpath("com.moowork.gradle:gradle-grunt-plugin:${gruntPluginVersion}")
    }
}

apply plugin: 'io.spring.dependency-management'

dependencyManagement {
    imports {
        mavenBom("io.spring.platform:platform-bom:${springPlatformVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'maven-publish'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.grunt'

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}



dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.hsqldb:hsqldb')
    compile('org.apache.commons:commons-lang3')
    compile('commons-io:commons-io')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('org.springframework.security:spring-security-test')
}

node {
    version = project.nodeVersion
    npmVersion = project.npmVersion
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    npmCommand = 'npm --no-color'

    workDir = rootProject.file("./frontend/nodejs")
    nodeModulesDir = rootProject.file("./frontend/")
}

task removeNodeModules(type: NpmTask, group: 'node') {
    it.workingDir = rootProject.file("./frontend/")
    args = ['run', 'removeNodeModules']
}

grunt {
    workDir = rootProject.file("${project.projectDir}/frontend/")
    colors = false
    bufferOutput = false
}

task gruntBuild(type: GruntTask, group: 'grunt', dependsOn: ['installGrunt', 'npmInstall']) {
    args = ['build']
}

task gruntEnd2End(type: GruntTask, group: 'grunt', dependsOn: ['build']) {
    args = ['e2eBuild']
}

task javascriptUnitTest(type: GruntTask, group: 'grunt', dependsOn: ['installGrunt', 'npmInstall']) {
    args = ['unitTest']
}

task javascriptIntegrationTest(type: GruntTask, group: 'grunt', dependsOn: ['installGrunt', 'npmInstall']) {
    args = ['javascriptIntegrationTest']
}


test {
    testLogging {
        exceptionFormat = 'full'
    }

    afterTest { desc, result ->
        println "- ${desc.name}: ${result.resultType}"
    }
}

test.finalizedBy javascriptUnitTest

checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle-gradle.xml')
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
}

task sourceJar(type: Jar, group: 'build', description: 'Write more descriptions') {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = project.name
            version = project.version


            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = project.name
    version = project.version
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task superClean(dependsOn: ['clean', 'removeNodeModules'], group: 'build', description: 'Write more descriptions')

jar.dependsOn gruntBuild

check.finalizedBy gruntEnd2End